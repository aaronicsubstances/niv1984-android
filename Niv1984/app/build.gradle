plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def defaultPropertiesFile = project.file("app.properties")

def debugProperties = new Properties()
debugProperties.load(new FileInputStream(defaultPropertiesFile))
def debugPropertiesFile = project.file("app-debug.properties")
if (debugPropertiesFile.exists()) {
    debugProperties.load(new FileInputStream(debugPropertiesFile))
}

def releaseProperties = new Properties()
releaseProperties.load(new FileInputStream(defaultPropertiesFile))
def releasePropertiesFile = project.file("app-release.properties")
if (releasePropertiesFile.exists()) {
    releaseProperties.load(new FileInputStream(releasePropertiesFile))
}

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    namespace "com.aaronicsubstances.niv1984"
    compileSdk 34

    defaultConfig {
        applicationId "com.aaronicsubstances.niv1984"
        minSdk 21
        targetSdk 34
        versionCode 222
        versionName "2.2.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'

            buildConfigField("String", "API_BASE_URL", "\"${debugProperties['API_BASE_URL']}\"")
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            buildConfigField("String", "API_BASE_URL", "\"${releaseProperties['API_BASE_URL']}\"")
        }
    }
    buildFeatures {
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

tasks.withType(JavaCompile).forEach {
    options.compilerArgs += ['-Xlint:deprecation']
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.2.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    implementation platform('com.google.firebase:firebase-bom:33.1.2')
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-firestore'

    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    testImplementation 'junit:junit:4.13.2'
}
